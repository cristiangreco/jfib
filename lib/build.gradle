plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'cc.regolo'
            artifactId = 'jfib'
            version = project.properties['jfib_version']

            artifacts = [jar, javadocJar, sourcesJar]

            pom {
                name = 'jfib'
                description = 'sample fibonacci java lib'
                url = 'https://github.com/cristiangreco/jfib'

                scm {
                    connection = 'scm:git:git://github.com/cristiangreco/jfib.git'
                    developerConnection = 'scm:git:ssh://github.com/cristiangreco/jfib.git'
                    url = 'http://github.com/cristiangreco/jfib'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'

            credentials {
                username = System.getenv('OSSRH_USERNAME')
                password = System.getenv('OSSRH_PASSWORD')
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
    useInMemoryPgpKeys(System.getenv('SIGNING_KEYID'), System.getenv('SIGNING_KEY'), System.getenv('SIGNING_PASSWORD'))
}
